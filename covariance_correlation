// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© jbmt

//@version=5
indicator("Correlation")
// Get user input 
lookback    = input.int(title="Lookback", defval=24) 
source      = input.source(title="Source", defval=close)
reference   = input.symbol(title="Reference Market", defval="USI:TICK")
useScaleAdjust = input.bool(defval=false, title="Use Scale Adjustment?", tooltip="Enables the reference market number to be divided by the scale adjust number")
scaleAdjust = input.float(title="Scale Adjustment", defval=10, tooltip="Number to divide reference market by in oreder to keep scale close to covariance number")

// Get % change of reference data source
referenceData = request.security(reference, timeframe.period, source)
referenceChange = useScaleAdjust ? (((referenceData - referenceData[1]) / referenceData[1]) * 100) / scaleAdjust : ((referenceData - referenceData[1]) / referenceData[1]) * 100

// Get % change of current market
currentData = source
currentChange = ((currentData - currentData[1]) / currentData[1]) * 100

// Declare arrays
var referenceArray = array.new_float(lookback)
var currentArray = array.new_float(lookback)

// Shift (remove) the last (first entered) value from our arrays (FIFO)
array.shift(referenceArray)
array.shift(currentArray)

// Add the current values to our arrays
array.push(referenceArray, referenceChange)
array.push(currentArray, currentChange)

// Determine & plot our covariance relationship
covariance = array.covariance(currentArray, referenceArray, true)
plot(covariance, color=color.purple, style=plot.style_area, title="Covariance")

// Plot our reference data
plot(referenceChange, color=color.new(color.red,25), style=plot.style_columns, title="Reference Market")
plot(currentChange, color=color.blue, style=plot.style_histogram, linewidth=4, title="Current Market")

// Determine the standard deviation of our arrays
referenceDev = array.stdev(referenceArray)
currentDev = array.stdev(currentArray)
correlation = covariance / (referenceDev * currentDev)
plot(correlation, color=color.black, linewidth=2, style=plot.style_stepline, title="Correlation Strength")
hline(0, title="0", color=color.blue, linestyle=hline.style_dotted, linewidth=1)
